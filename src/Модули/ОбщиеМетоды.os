Функция ОбъектВСтруктуру(Объект) Экспорт
	Рефлектор = Новый Рефлектор;
	Структура = Новый Структура;
	Если Объект = Неопределено Тогда
		Возврат Структура;
	КонецЕсли;
	Для каждого Свойство Из Рефлектор.ПолучитьТаблицуСвойств(ТипЗнч(Объект)) Цикл
		Аннотации = Свойство.Аннотации;
		Если Не Аннотации.Найти("КоллекцияОбъектов", "Имя") = Неопределено Тогда 
			ЗначениеСвойства = КоллекциюВСтруктуры(Объект[Свойство.Имя]);
		ИначеЕсли Не Аннотации.Найти("Объект", "Имя") = Неопределено Тогда
			ЗначениеСвойства = ОбъектВСтруктуру(Объект[Свойство.Имя]);
		Иначе
			ЗначениеСвойства = Объект[Свойство.Имя];
		КонецЕсли;
		Если ЗначениеЗаполнено(ЗначениеСвойства) Или ТипЗнч(ЗначениеСвойства) = Тип("Булево") Тогда
			Структура.Вставить(Свойство.Имя, ЗначениеСвойства);
		КонецЕсли;
	КонецЦикла;
	Возврат Структура;
КонецФункции

Функция КоллекциюВСтруктуры(Коллекция)
	Результат = Новый Массив;
	Для каждого Объект Из Коллекция Цикл
		Результат.Добавить(ОбъектВСтруктуру(Объект));
	КонецЦикла;
	Возврат Результат;
КонецФункции
