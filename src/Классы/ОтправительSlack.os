#Использовать json

Перем WebhookURL;

// Конструктор объекта ОтправительSlack.
//
Процедура ПриСозданииОбъекта(АдресВебХука = "")
	WebhookURL = АдресВебХука;
КонецПроцедуры

#Область ПрограммныйИнтерфейс
Функция АдресВебХука(Значение) Экспорт
	WebhookURL = Значение;
	Возврат ЭтотОбъект;
КонецФункции

Функция Отправить(Сообщение) Экспорт

	СтруктураСообщения = ОбщиеМетоды.ОбъектВСтруктуру(Сообщение);
	Парсер = Новый ПарсерJSON;
	JSON = Парсер.ЗаписатьJSON(СтруктураСообщения);
	
	Соединение = Новый HTTPСоединение(WebhookURL, 443, , , Новый ИнтернетПрокси(Истина), 30);

	Запрос = Новый HTTPЗапрос;
	Запрос.АдресРесурса = WebhookURL;
	Запрос.Заголовки.Вставить("Content-Type", "application/json");
	Запрос.УстановитьТелоИзСтроки(JSON);

	Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Если Ответ.КодСостояния > 299 Тогда
		СтрокаОшибки = СтрШаблон("Не удалось отправить сообщение: %1", Ответ.ПолучитьТелоКакСтроку());
		ВызватьИсключение СтрокаОшибки;
	КонецЕсли;

	Возврат ЭтотОбъект;

КонецФункции

#КонецОбласти