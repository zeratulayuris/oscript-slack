Перем text Экспорт;
Перем blocks Экспорт;
Перем attachments Экспорт;
Перем thread_ts Экспорт;
Перем mrkdwn Экспорт;

Перем username Экспорт;
Перем icon_url Экспорт;
Перем icon_emoji Экспорт;
Перем channel Экспорт;

#Область ПрограммныйИнтерфейс

// Устанавливает основной текст сообщения
//
// Параметры:
//   Значение - Строка - текст сообщения
//
Функция Текст(Знач Значение) Экспорт
	text = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// НЕ РЕАЛИЗОВАНО. Добавляет новый блок к сообщению.
//
// Параметры:
//   Блок - Тип - 
//
Функция ДобавитьБлок(Блок) Экспорт
	
	ВызватьИсключение "Не реализовано!";
		
КонецФункции

// Добавляет вложение к сообщению. Deprecated.
//
// Параметры:
//   Вложение - ВложениеSlack - добавляемое вложение.
//
Функция ДобавитьВложение(Вложение) Экспорт
	
	Если ТипЗнч(Вложение) = Тип("ВложениеSlack") Тогда
		attachments.Добавить(Вложение);
	Иначе
		ВызватьИсключение "Неверный тип вложения!";
	КонецЕсли;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает идентификатор родительского сообщения для ответа в тред.
//
// Параметры:
//   Значение - Строка - идентификатор родителя.
//
Функция ИдентификаторРодителя(Знач Значение) Экспорт
	thread_ts = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает использование разметки mrkdwn для сообщения.
// По умолчанию установлено в Истина.
//
// Параметры:
//   Значение - Булево - Истина: использовать разметку.
//
Функция ИспользоватьРазметку(Знач Значение) Экспорт
	mrkdwn = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает отображаемое имя пользователя Slack.
//
// Параметры:
//   Значение - Строка - отображаемое имя
//
Функция ИмяПользователя(Знач Значение) Экспорт
	username = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает ссылку на файл, содержащий значок пользователя Slack.
// Работает только при использовании интеграции Incoming Webhooks.
//
// Параметры:
//   Значение - Строка - URL значка
//
Функция УРЛЗначка(Знач Значение) Экспорт
	icon_url = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает эмодзи, который будет отображен вместо аватарки пользователя Slack.
// Работает только при использовании интеграции Incoming Webhooks.
//
// Параметры:
//   Значение - Строка - эмодзи, который нужно отобразить (напр. :hammer:)
//
Функция ЭмодзиЗначка(Знач Значение) Экспорт
	icon_emoji = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает имя канала, в который будет отправлено сообщение.
// Для отправки в личный сообщения пользователя указать @username.
// Работает только при использовании интеграции Incoming Webhooks.
//
// Параметры:
//   Значение - Строка - имя канала
//
Функция Канал(Знач Значение) Экспорт
	channel = Значение;
	Возврат ЭтотОбъект;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ОбъектныеСвойства() Экспорт
	Свойства = Новый Массив;
	Свойства.Добавить("attachments");
	Возврат Свойства;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриСозданииОбъекта(Текст = "")
	
	text = Текст;
	blocks = Новый Массив;
	attachments = Новый Массив;
	mrkdwn = Истина;
	
КонецПроцедуры

#КонецОбласти
