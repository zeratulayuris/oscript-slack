Перем fallback Экспорт;
Перем color Экспорт;
Перем pretext Экспорт;

Перем author_name Экспорт;
Перем author_link Экспорт;
Перем author_icon Экспорт;

Перем title Экспорт;
Перем title_link Экспорт;

Перем text Экспорт;

Перем fields Экспорт;

Перем image_url Экспорт;
Перем thumb_url Экспорт;

Перем footer Экспорт;
Перем footer_icon Экспорт;

Перем ts Экспорт;

#Область ПрограммныйИнтерфейс

// Добавляет новое поле к вложению
//
// Параметры:
//   Заголовок - Строка - заголовок поля (title)
//   Значение - Строка(800) - значение поля (value)
//   Короткое - Булево - флаг, указывающий, что значение поля достаточно короткое для вывода в одну строку (short)
//
Функция ДобавитьПоле(Знач Заголовок, Знач Значение = "", Знач Короткое = Ложь) Экспорт
	
	fields.Добавить(НовоеПоле(Заголовок, Значение, Короткое));
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает fallback-текст вложения
//
// Параметры:
//   Значение - Строка - fallback-текст, не может содержать форматирование
//
Функция КраткийТекст(Знач Значение) Экспорт
	fallback = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает цвет боковой полоски вложения
//
// Параметры:
//   Значение - Строка - имя (good, warning, danger) или hex (#FF38FF) цвета
//
Функция Цвет(Знач Значение) Экспорт
	color = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает pretext вложения - то, что будет отображено над блоком вложения
//
// Параметры:
//   Значение - Строка - претекст
//
Функция Претекст(Знач Значение) Экспорт
	pretext = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает author_name
//
// Параметры:
//   Значение - Строка - имя автора
//
Функция ИмяАвтора(Знач Значение) Экспорт
	author_name = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает author_link - учитывается только при установленном имени автора
//
// Параметры:
//   Значение - Строка - URL-указатель на автора
//
Функция СсылкаАвтора(Знач Значение) Экспорт
	author_link = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает author_icon - ссылку на маленькую (16x16px) иконку автора
//
// Параметры:
//   Значение - Строка - URL-указатель на картинку
//
Функция ИконкаАвтора(Знач Значение) Экспорт
	author_icon = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает заголовок вложения - будет отображен увеличенным жирным шрифтом наверху вложения
//
// Параметры:
//   Значение - Строка - URL-указатель на картинку
//
Функция Заголовок(Знач Значение) Экспорт
	title = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает ссылку заголовка вложения - сделает заголовок гиперссылкой.
// Работает только если заголовок установлен.
//
// Параметры:
//   Значение - Строка - URL-указатель
//
Функция СсылкаЗаголовка(Знач Значение) Экспорт
	title_link = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает основной текст вложения. Ссылки внутри этого текста не будут разворачиваться.
// Если в тексте 700+ знаков или 5+ переводов строки, текст будет показан свёрнутым.
//
// Параметры:
//   Значение - Строка - текст вложения
//
Функция Текст(Знач Значение) Экспорт
	text = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает ссылку картинки вложения.
// Поддерживаются форматы GIF, JPEG, PNG или BMP.
//
// Параметры:
//   Значение - Строка - URL-указатель на картинку
//
Функция СсылкаКартинки(Знач Значение) Экспорт
	image_url = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает ссылку картинки-превью, которая будет отображена справа от текста вложения
// Поддерживаются форматы GIF, JPEG, PNG или BMP.
// Максимальный размер - 75x75px
//
// Параметры:
//   Значение - Строка - URL-указатель на картинку
//
Функция СсылкаПревью(Знач Значение) Экспорт
	thumb_url = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает текст подвала вложения
//
// Параметры:
//   Значение - Строка - текст подвала
//
Функция Подвал(Знач Значение) Экспорт
	footer = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает URL иконки подвала (16x16px)
//
// Параметры:
//   Значение - Строка - URL-указатель на картинку
//
Функция ИконкаПодвала(Знач Значение) Экспорт
	footer_icon = Значение;
	Возврат ЭтотОбъект;
КонецФункции

// Устанавливает временную метку события.
// Если передать Число - оно считается датой от начала эпохи unix в секундах
// Если передать Дата - она считается универсальной датой UTC
//
// Параметры:
//   Значение - Число, Дата - УниверсальнаяДата (время UTC) либо Число - unix epoch time
//
Функция ВремяСобытия(Знач Значение) Экспорт
	ТипЗначения = ТипЗнч(Значение);
	Если ТипЗначения = Тип("Число") Тогда
		ts = Значение;
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		ts = Значение - Дата(1970, 1, 1); // начало эпохи unix
	Иначе
		ВызватьИсключение "Неверный тип данных: " + Строка(ТипЗначения);
	КонецЕсли;
	Возврат ЭтотОбъект;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовоеПоле(Знач title, Знач value, Знач short)

	Структура = Новый Структура;
	Структура.Вставить("title", title);
	Структура.Вставить("value", value);
	Структура.Вставить("short", short);
	Возврат Структура;
	
КонецФункции

Процедура ПриСозданииОбъекта(Знач КраткийТекст = "", Знач Текст = "", Знач Претекст = "", Знач Цвет = "")
	
	fallback = КраткийТекст;
	text = Текст;
	pretext = Претекст;
	color = Цвет;
	fields = Новый Массив;
	
КонецПроцедуры

#КонецОбласти
